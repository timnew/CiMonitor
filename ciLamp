#!/usr/bin/env coffee

request = require('superagent')
LedLamp = require('./PatternedLedLamp')
patterns = require('./LedPatterns')

options = require('optimist')
  .usage('Cycle LED light color in Hue.\nUsage: ciLamp [options] <url>\n$0')
  .options 'p'
    alias: 'port'
    default: null
    type: 'string'
    description: 'The name of the port connected to Arduio board, leave empty for auto-detect'
  .options 'i'
    alias: 'interval'
    default: '1000'
    type: 'string'
    description: 'The query interval(ms) (default: 1000ms)'
  .options 'U'
    alias: 'user'
    default: null
    type: 'string'
    description: 'User to log into CI server'
  .options 'P'
    alias: 'password'
    default: null
    type: 'string'
    description: 'Password to log into CI server'
  .argv

url = options._[0] ? 'https://jenkins.fishsource.org/job/JS%20Spec/api/json'

options.interval = parseInt(options.interval, 10);

LedLamp.findOrSetByName options.port, (err, led) ->
  return console.error err if err?
  
  console.log "Connecting to port #{led.portName}..."
  led.ready ->
    console.log "LED Connected"
    
    lastState = null

    update = ->
      request
        .get(url)
        .auth(options.user, options.password)
        .end (err, res) ->
          return if err?

          currentState = res.body.color
          
          return if currentState == lastState
          
          lastState = currentState
          
          process.stdout.write "\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b"
          process.stdout.write "\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b\b"
          
          process.stdout.write "Status: "
          
          switch currentState
            when 'red_anime', 'yellow_anime', 'blue_anime', 'grey_anime', 'disabled_anime', 'aborted_anime', 'notbuilt_anime'
              led.applyPattern patterns.HueMarquee, {}
              process.stdout.write "In progress..."
            when 'red'
              led.applyPattern patterns.Constant, 
                color: [255, 0, 0]
              process.stdout.write "Failed"
            when 'yellow'
              led.applyPattern patterns.Constant,
                color: [128, 0, 128]
              process.stdout.write "Unstable"
            when 'blue'
              led.applyPattern patterns.Constant,
                color: [0, 255, 0]
              process.stdout.write "Passed"
            when 'grey', 'disabled', 'aborted',' notbuilt'
              led.applyPattern patterns.Blink, 
                color: [64, 64, 64]
                duration: 100
              process.stdout.write "No Result"  

    setInterval update, options.interval 



