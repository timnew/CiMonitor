#!/usr/bin/env coffee

request = require('superagent')
LedLamp = require('./LedLamp')

options = require('optimist')
  .usage('Cycle LED light color in Hue.\nUsage: ciLamp [options] <url>\n$0')
  .options 'p'
    alias: 'port'
    default: null
    type: 'string'
    description: 'The name of the port connected to Arduio board, leave empty for auto-detect'
  .options 'i'
    alias: 'interval'
    default: '1000'
    type: 'string'
    description: 'The query interval(ms) (default: 1000ms)'
  .options 'U'
    alias: 'user'
    default: null
    type: 'string'
    description: 'User to log into CI server'
  .options 'P'
    alias: 'password'
    default: null
    type: 'string'
    description: 'Password to log into CI server'
  .argv

url = options._[0] ? 'https://jenkins.fishsource.org/job/JS%20Spec/api/json'

options.interval = parseInt(options.interval, 10);


parseColor = (color) ->
  # https://github.com/jenkinsci/jenkins/blob/master/core/src/main/java/hudson/model/BallColor.java#L56
  switch color
    when 'red', 'red_anime'
      'red'
    when 'yellow', 'yellow_anime'
      'yellow'
    when 'blue', 'blue_anime'
      'blue'
    when 'grey', 'grey_anime'
      'grey'
    when 'disabled', 'disabled_anime'
      'grey'
    when 'aborted', 'aborted_anime'
      'grey'
    when 'notbuilt', 'notbuilt_anime'
      'grey'
      
LedLamp.findOrSetByName options.port, (err, led) ->
  return console.error err if err?
  
  console.log "Connecting to port #{led.portName}..."
  led.ready ->
    console.log "LED Connected"
    
    update = ->
      request
        .get(url)
        .auth(options.user, options.password)
        .end (err, res) ->
          return if err?
          console.log res.body.color
          led.setColorKeyword parseColor(res.body.color)
      
    setInterval update, options.interval 



